///////////////////////////////////////////////////////////////////////////////
// 
// Служебный модуль с реализацией сценария обработки файлов <СортировкаСоставаОпределяемыхТипов>
//
///////////////////////////////////////////////////////////////////////////////

// ИмяСценария
//	Возвращает имя сценария обработки файлов
//
// Возвращаемое значение:
//   Строка   - Имя текущего сценария обработки файлов
//
Функция ИмяСценария() Экспорт
	
	Возврат "СортировкаСоставаОпределяемыхТипов";
	
КонецФункции // ИмяСценария()

// ОбработатьФайл
//	Выполняет обработку файла
//
// Параметры:
//  АнализируемыйФайл		- Файл - Файл из журнала git для анализа
//  КаталогИсходныхФайлов  	- Строка - Каталог расположения исходных файлов относительно каталог репозитория
//  ДополнительныеПараметры - Структура - Набор дополнительных параметров, которые можно использовать 
//  	* Лог  					- Объект - Текущий лог
//  	* ИзмененныеКаталоги	- Массив - Каталоги, которые необходимо добавить в индекс
//		* КаталогРепозитория	- Строка - Адрес каталога репозитория
//		* ФайлыДляПостОбработки	- Массив - Файлы, изменившиеся / образовавшиеся в результате работы сценария
//											и которые необходимо дообработать
//
// Возвращаемое значение:
//   Булево - Признак выполненной обработки файла
//
Функция ОбработатьФайл(АнализируемыйФайл, КаталогИсходныхФайлов, ДополнительныеПараметры) Экспорт
	
	Лог = ДополнительныеПараметры.Лог;
	НастройкиСценария = ДополнительныеПараметры.Настройки.Получить(ИмяСценария());
	УчитываяПрефикс = НастройкиСценария.Получить("УчитываяПрефикс");
	
	ФайлОбработан = Ложь;

	Если АнализируемыйФайл.Существует() Тогда
		
		ЭтоОпределяемыйТипЕДТ = ТипыФайлов.ЭтоФайлОписанияОпределяемогоТипаEDT(АнализируемыйФайл);
		Если ЭтоОпределяемыйТипЕДТ Или ТипыФайлов.ЭтоФайлОписанияОпределяемогоТипа(АнализируемыйФайл) Тогда
		
			Лог.Информация("Обработка файла '%1' по сценарию '%2'", АнализируемыйФайл.ПолноеИмя, ИмяСценария());

			ФайлОбработан = ОтсортироватьОпределяемыйТип(АнализируемыйФайл.ПолноеИмя, ЭтоОпределяемыйТипЕДТ, УчитываяПрефикс);
			
			Если ФайлОбработан Тогда
				ДополнительныеПараметры.ИзмененныеКаталоги.Добавить(АнализируемыйФайл.ПолноеИмя);
			КонецЕсли;

		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ФайлОбработан;
	
КонецФункции // ОбработатьФайл()

Функция ОтсортироватьОпределяемыйТип(Знач ИмяФайла, ЭтоЕДТ, УчитываяПрефикс)

	СодержимоеФайла = ФайловыеОперации.ПрочитатьТекстФайла(ИмяФайла);

	ИмяТега = ?(ЭтоЕДТ,
		"types",
		"v8:Type");
	РазделительЭлементов = ?(ЭтоЕДТ,
		Символы.ПС + "    ",
		Символы.ПС + "				");
	
	Если РегулярныеВыражения.СортироватьМассивТеговСостава(СодержимоеФайла, ИмяТега, РазделительЭлементов, УчитываяПрефикс) Тогда
		ФайловыеОперации.ЗаписатьТекстФайла(ИмяФайла, СодержимоеФайла);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
